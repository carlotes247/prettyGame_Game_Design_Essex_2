BasicGame no_players=2
  SpriteSet
    floor > Immovable img=oryx/backLBlue hidden=True
    goal  > Door color=GREEN img=oryx/doorclosed1

    movable >
      avatar > ShootAvatar ammo=pickaxe img=oryx/knight1 imgUp=oryx/alien1  imgDown=oryx/alien2 imgLeft=oryx/alien3 imgRight=oryx/angel1
        avatarA > stype=proA
        avatarB > stype=proB
      enemy > RandomNPC cooldown=3 cons=15 img=oryx/bat1 speed=0.2
      tank > Missile speed=0.05
        tankR > img=newset/tank_blueR orientation=RIGHT
        tankU > img=newset/tank_blue orientation=UP

    pickaxe > Resource value=1 limit=5 img=oryx/pickaxe

    wall > Immovable img=oryx/wall3 autotiling=True

    flighttime > Resource value=1 limit=5
    pro > Missile speed=1 img=oryx/pickaxe
        proA >
        proB >

    win > Immovable invisible=true
        winA >
        winB >

  LevelMapping
    g > floor goal
    A > floor avatarA
    B > floor avatarB
    e > floor enemy
    . > floor
    w > floor wall
    t > floor tankR
    o > floor tankU
    p > floor pickaxe

  InteractionSet

    pro wall > stepBack
    pro wall > reverseDirection

    pro floor > changeResource resource=flighttime value=1
    pro floor > spawnIfHasMore stype=pickaxe resource=flighttime limit=4
    pro floor > killIfHasMore resource=flighttime limit=4

    pro tank > killSprite
    tank pro > spawn stype=pickaxe
    tank pro > bounceForward

    avatar pickaxe > changeResource resource=pickaxe
    pickaxe avatar > killSprite
    wall avatar > killIfOtherHasMore resource=pickaxe limit=1 subtract=true

    pro EOS > wrapAround

    enemy pro > spawn stype=pickaxe
    pro enemy > killSprite
    enemy pro > killSprite

    avatarB proA > killSprite
    avatarA proB > killSprite

    wall tank > killSprite
    movable wall > stepBack
    movable EOS > wrapAround
    goal avatarA > transformTo stype=winA
    goal avatarB > transformTo stype=winB
    enemy enemy > stepBack
    avatar enemy tank > killSprite scoreChange=-1

  TerminationSet
    SpriteCounterMore stype=winA limit=1  win=True,False
    SpriteCounterMore stype=winB limit=1   win=False,True
    SpriteCounter stype=avatarA win=False,True
    SpriteCounter stype=avatarB win=True,False