GameSpace square_size=30
  SpriteSet
    floor > Immovable img=world_NES_sliced/58 hidden=True
    floor2 > Immovable img=world_NES_sliced/2  hidden=True
    goal  > Door color=GREEN img=world_NES_sliced/0

    movable >
      avatar  > ShootAvatar ammo=pickaxe speed=AVSPEED frameRate=1.25 stype=pro orientedImg=Link_NES_sliced/link
      enemy > RandomNPC cooldown=3 cons=BATCONS img=oryx/bat1 speed=BATSPEED
      tank > Missile speed=TANKSPEED
        tankR > img=newset/tank_blueR orientation=RIGHT
        tankU > img=newset/tank_blue orientation=UP
        tankL > img=newset/tank_blueL orientation=LEFT
        tankD > img=newset/tank_blueD orientation=DOWN

    #Tick counter
    #Quiescent tick counter
    pcq > Immovable invisible=True hidden=True
    #Active tick counter
    pca > SpawnPoint stype=pc invisible=True hidden=True
    #Tick count
    pc > Immovable invisible=True hidden=True
    #Termination state
    dead > Immovable invisible=True hidden=True

    paspawn > SpawnPoint stype=pam prob=0.1 cooldown=PAXECD invisible=True hidden=True
    pam > SpawnPoint stype=pickaxe prob=1 total=1 invisible=True hidden=True
    pickaxe > Resource value=1 limit=LPAXE img=oryx/pickaxe

    coin > Resource value=1

    wall > Immovable img=world_NES_sliced/wall1
    pro > Missile speed=1 img=oryx/pickaxe singleton=True

  LevelMapping
    g > floor goal
    A > floor avatar pcq
    e > floor enemy
    . > floor
    * > floor2
    w > floor wall
    c > floor wall coin
    $ > floor coin
    r > floor tankR
    u > floor tankU
    l > floor tankL
    d > floor tankD
    p > floor pickaxe paspawn

  InteractionSet
    #if a pickaxe would respawn on a tile that already has one, prevent it
    pam pickaxe > killSprite
    wall pro > killSprite

    tank pro > bounceForward
    pro tank > transformTo stype=pickaxe

    pickaxe avatar > collectResource scoreChange=1
    pickaxe avatar > killSprite

    pro EOS > wrapAround

    pro enemy > transformTo stype=pickaxe
    enemy pro > killSprite

    wall tank > killSprite
    movable wall > stepBack
    movable EOS > wrapAround
    goal avatar > killSprite
    enemy enemy > stepBack
    avatar enemy tank > killSprite scoreChange=-1

    #pickaxe flight timer
    pcq floor > transformIfCount stype=pca stypeCount=pro geq=1
    pca floor > transformIfCount stype=pcq stypeCount=pc geq=2
    pro floor > transformIfCount stype=pickaxe stypeCount=pc geq=2
    pc floor > transformIfCount stype=dead stypeCount=pc geq=2

    pca floor > transformIfCount stype=pcq stypeCount=pro leq=0
    pc floor > transformIfCount stype=dead stypeCount=pro leq=0

    dead floor > killSprite

    coin avatar > killSprite scoreChange=5

  TerminationSet
    SpriteCounter stype=goal   win=True
    SpriteCounter stype=enemy  win=True
    SpriteCounter stype=avatar win=False
    Timeout limit=500 win=False


  ParameterSet
    #{Name of the parameter} > {values(min, inc, max)/(boolean)} {descriptive string}

    AVSPEED      > values=0.1:0.5:1.0     string=Avatar_Speed
    BATSPEED      > values=0.1:0.2:0.5     string=Bat_Speed
    BATCONS       > values=10:5:20   string=Bat_Cons_Moves
    TANKSPEED       > values=0:0.1:0.2          string=Tank_Speed
    PAXECD      > values=15:5:25     string=Pickaxe_Cooldown
    LPAXE      > values=1:1:3     string=Pickaxe_Limit
    #VPAXE      > values=1:1:3     string=Pickaxe_Value