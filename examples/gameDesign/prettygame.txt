GameSpace square_size=50
  SpriteSet
    floor > Immovable img=world_NES_sliced/58 hidden=True
    floor2 > Immovable img=world_NES_sliced/2  hidden=True
    goal  > Door color=GREEN img=world_NES_sliced/0

    movable >
      avatar  > ShootAvatar  rotateInPlace=false ammo=pickaxe speed=1 frameRate=1.25 stype=pro orientedImg=Link_NES_sliced/link
      #enemy > Chaser stype=avatar img=oryx/bat1 speed=BATSPEED
      tank > Missile speed=TANKSPEED
        tankR > img=newset/tank_blueR orientation=RIGHT
        tankU > img=newset/tank_blue orientation=UP
        tankL > img=newset/tank_blueL orientation=LEFT
        tankD > img=newset/tank_blueD orientation=DOWN

    #Tick counter
    #Quiescent tick counter
    pcq > Immovable invisible=True hidden=True
    #Active tick counter
    pca > SpawnPoint stype=pc invisible=True hidden=True
    #Tick count
    pc > Immovable invisible=True hidden=True
    #Termination state
    dead > Immovable invisible=True hidden=True

    timebonus > SpawnPoint stype=timebonus_tick invisible=true hidden=True
    timebonus_limit > SpawnPoint stype=timebonus_maxticks cooldown=700 invisible=true hidden=True
    timebonus_tick > Immovable invisible=true hidden=True
    timebonus_maxticks > Immovable invisible=true hidden=True
    timebonus_start > Immovable invisible=true hidden=True

    pickaxe_penalty > Immovable invisible=true hidden=True
    pickaxe_penalty2 > Immovable invisible=true hidden=True
    pickaxe_penalty3 > Immovable invisible=true hidden=True

    paspawn > SpawnPoint stype=pam prob=0.1 cooldown=PAXECD invisible=True hidden=True
    pam > SpawnPoint stype=pickaxe prob=1 total=1 invisible=True hidden=True
    pickaxe > Resource value=1 limit=LPAXE img=oryx/pickaxe

    wall > Immovable img=world_NES_sliced/wall1
    pro > Missile speed=1 img=oryx/pickaxe singleton=True

    gold > Passive img=oryx/gold3

  LevelMapping
    g > floor goal
    A > floor avatar pcq pickaxe_penalty  timebonus timebonus_limit timebonus_start
    . > floor
    * > floor2
    w > floor wall
    r > floor tankR
    u > floor tankU
    l > floor tankL
    d > floor tankD
    p > floor pickaxe paspawn
    $ > floor gold

  InteractionSet
    #if a pickaxe would respawn on a tile that already has one, prevent it
    pam pickaxe > killSprite

    timebonus_tick timebonus > killSprite scoreChange=-1 enabled=TBONUS
    timebonus floor > transformIfCount stype=dead stypeCount=timebonus_maxticks geq=2 enabled=TBONUS
    timebonus_limit floor > transformIfCount stype=dead stypeCount=timebonus_maxticks geq=2 enabled=TBONUS
    timebonus_maxticks timebonus_maxticks > killBoth enabled=TBONUS
    timebonus_start timebonus > killSprite scoreChange=1000 enabled=TBONUS

    pickaxe_penalty floor > transformIfCount stype=pickaxe_penalty2 stypeCount=pro geq=1
    pickaxe_penalty2 floor > transformTo stype=pickaxe_penalty3 scoreChange=-2
    pickaxe_penalty3 floor > transformIfCount stype=pickaxe_penalty stypeCount=pro leq=0

    #pro enemy > transformTo stype=pickaxe
    #enemy pro > killSprite scoreChange=10

    wall pro > killBoth scoreChange=SC_WALL
    #pro wall > transformTo stype=pickaxe
    gold avatar > killSprite scoreChange=SC_GOLD

    #tank pro > bounceForward
    pro tank > killSprite

    pickaxe avatar > collectResource scoreChange=SC_PAXE
    pickaxe avatar > killSprite

    pro EOS > wrapAround

    wall tank > killSprite

    movable wall > stepBack
    movable EOS > wrapAround
    goal avatar > killSprite
    #enemy enemy > stepBack
    avatar tank > killSprite scoreChange=-100

    #pickaxe flight timer
    pcq floor > transformIfCount stype=pca stypeCount=pro geq=1
    pca floor > transformIfCount stype=pcq stypeCount=pc geq=2
    pro floor > transformIfCount stype=pickaxe stypeCount=pc geq=2
    pc floor > transformIfCount stype=dead stypeCount=pc geq=2

    pca floor > transformIfCount stype=pcq stypeCount=pro leq=0
    pc floor > transformIfCount stype=dead stypeCount=pro leq=0

    dead floor > killSprite

  TerminationSet
    SpriteCounter stype=goal   win=True
    #SpriteCounter stype=enemy  win=True
    SpriteCounter stype=avatar win=False
    Timeout limit=1000 win=False


  ParameterSet
    #{Name of the parameter} > {values(min, inc, max)/(boolean)} {descriptive string}

    TANKSPEED    > values=0:0.2:1         string=Tank_Speed
    PAXECD       > values=10:5:25         string=Pickaxe_Cooldown
    LPAXE        > values=1:1:3           string=Pickaxe_Limit
    VPAXE        > values=1:1:3           string=Pickaxe_Value
    TBONUS       > values=True:False      string=Time_Bonus
    SC_GOLD      > values=0:5:15          string=Score_Gold
    SC_PAXE      > values=0:5:15          string=Score_Pickaxe
    SC_WALL      > values=0:5:15          string=Score_Wall_Kill